import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id "de.undercouch.download" version "3.1.2"
}

group 'com.objectpartners.plummer'
version '1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'kotlin2js'

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}

sourceSets {
    main {
        kotlin {
            srcDir "${project(':kotlin-schema').projectDir.path}/src/main/kotlin"
        }
    }
}

/**
 * Compiles Kotlin files into JavaScript. This has been customized to delete files and regenerate
 * each time the task is run to avoid caching issues.
 */
compileKotlin2Js {
    doFirst {
        configurations.runtime.each { println it }
        new File("${buildDir}/generated-schema").delete()
    }
    kotlinOptions.outputFile = "${buildDir}/generated-schema/opi-weather-schema.js"
    kotlinOptions.moduleKind = "plain"
    kotlinOptions.verbose = true
    kotlinOptions.sourceMap = true
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-js-library:${kotlinVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
}

/**
 * Unzip the downloaded Kotlin JS Runtime JAR and extract 'kotlin.js' file for inclusion in the webapp
 */
task prepareKotlinJsRuntime(type: Copy, dependsOn: ['downloadKotlinJsRuntime']) {
    from zipTree(file("${projectDir}/lib/kotlin-js-${kotlinVersion}.jar"))
    into file("${projectDir}/../client/lib/kotlin-js")
}

/**
 * Download Kotlin JS Runtime JAR from public repo. Will only download if file doesn't already exist
 */
task downloadKotlinJsRuntime(type: Download) {
    overwrite false
    src "http://dl.bintray.com/kotlin/kotlin-eap-1.1/org/jetbrains/kotlin/kotlin-js-library/${kotlinVersion}/kotlin-js-library-${kotlinVersion}.jar"
    dest "${projectDir}/lib/kotlin-js-${kotlinVersion}.jar"
}